{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "300418ae",
   "metadata": {},
   "source": [
    "> **THE OWCF WEB APPLICATIONS ARE TEMPORARILY OUT-OF-ORDER!**"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f60e718b",
   "metadata": {},
   "source": [
    "# comWebApp\n",
    "\n",
    "## Description:\n",
    "This notebook provides an application to visualize orbits in a tokamak in an interactive\n",
    "and intuitive manner. The orbits are visualized as a function of the classical constants\n",
    "of motion (COM). They are energy (E), magnetic moment (mu) and toroidal canonical momentum (Pphi). The degeneracy for certain combination of EmuPphi is not adressed. Thus, for some EmuPphi-coordinates there will be two orbits plotted. The magnetic equilibrium is created from scratch with the help of the Solov'ev equilibrium formula from the book by Goedbloed. The parameters that can be specified in real-time are inverse aspect ratio, plasma elongation, triangularity, minor radius (in meters) and Bϕ on axis.\n",
    "\n",
    "### Please note\n",
    "Of all the apps in the OWCF, this might be the least developed one. It works, but there are some (hard-coded) lines into the application the cause limitations. For example, on line 80 and 84 are hard-coded initial limit values for the mu and Pphi sliders.\n",
    "\n",
    "## Inputs:\n",
    "- m - Mass of particle. In kg. - Float64\n",
    "- q - Charge of particle. In Coloumb. - Float64\n",
    "- Emin - The minimum fast-ion energy value to be accessible. keV - Float64\n",
    "- Emax - The maximum fast-ion energy value to be accessible. keV - Float64\n",
    "- verbose - If set to true, the app will talk a lot! - Bool\n",
    "- port - The I/O through which the app will be accessed via a web browser - Int64\n",
    "\n",
    "## Outputs\n",
    "#### -\n",
    "\n",
    "## Saved files\n",
    "#### -\n",
    "\n",
    "## Notebook written by Henrik Järleblad, henrikj@dtu.dk\n",
    "## Last maintained 2025-10-07"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b5bfa00e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# SPECIFY THE INPUTS IN THIS CELL\n",
    "\n",
    "# Please specify the OWCF folder and let the app change directory to the \n",
    "# OWCF folder when comWebApp.jl is executed. This is to be able to load the\n",
    "# correct versions of the Julia packages as specified in the Project.toml and \n",
    "# Manifest.toml files.\n",
    "folderpath_OWCF = \"/path/to/the/OWCF/\" # Finish with '/'\n",
    "\n",
    "m = 2.0*1.660539e-27 # Mass in kg. Change manually here. Example value alpha particle: 4.001506179127 .* 1.660539e-27\n",
    "q = 2.0 * (1.602176e-19) # Charge in Coulombs. Change manually here. Example value alpha particle: 2.0 * (1.602176e-19)\n",
    "Emin = 10.0 # keV\n",
    "Emax = 2000.0 # KeV\n",
    "Bmin = 2.0 # Minimum magnetic field value in the tokamak. Tesla. Example JET approx.: 2.0\n",
    "verbose = true"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9db7aac9",
   "metadata": {},
   "outputs": [],
   "source": [
    "# RUN THE APP BY FIRST SELECTING THE ABOVE CELL AND PRESS Ctrl+Enter. THEN SELECT THIS CELL\n",
    "# AND PRESS Shift+Enter. MIGHT TAKE A MOMENT TO PREPARE+LOAD THE INTERACTIVE APP.\n",
    "\n",
    "cd(folderpath_OWCF)\n",
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "verbose && println(\"Loading packages... \")\n",
    "using GuidingCenterOrbits\n",
    "using Interact\n",
    "using Plots\n",
    "using LinearAlgebra\n",
    "using Contour\n",
    "\n",
    "## ------\n",
    "verbose && println(\"Defining μ function... \")\n",
    "μ_func(E, B, Pϕ, Ψ, RBϕ) = begin\n",
    "    res = E/B - (B/(2*m)) * ((Pϕ-q*Ψ)/RBϕ)^2\n",
    "    (res > 0.0) ? res : 0.0\n",
    "end\n",
    "verbose && println(\"Done!\")\n",
    "\n",
    "## ------\n",
    "## Approximate ranges. Need better method of determining them.\n",
    "println(\"Calculating range of E, μ and Pϕ values... \")\n",
    "E_array = round.(collect(range(Emin,stop=Emax, length=500)),sigdigits=3) # keV\n",
    "vmax = sqrt(maximum(E_array)*1000*q/m) # 1000*q is to convert from keV to Joule. Then sqrt(E/m) to obtain speed.\n",
    "Bmin = Bmin\n",
    "μ_max = (m*(vmax^2))/(2*minimum(Bmin))\n",
    "μ_array = round.(collect(range(0.0,stop=μ_max,length=50)),sigdigits=3)\n",
    "\n",
    "Pϕ_max = q * 2.6 + ((1/0.4)+1.0) * m * vmax # 2.6 is assumed for Ψ_LCFS. \n",
    "Pϕ_min = -((1/0.4)+1.0) * m * vmax # To be optimized\n",
    "Pϕ_array = round.(collect(range(Pϕ_min,stop=Pϕ_max,length=50)),sigdigits=3)\n",
    "\n",
    "println(\"Pϕ_min: $(Pϕ_min)\")\n",
    "println(\"Pϕ_max: $(Pϕ_max)\")\n",
    "\n",
    "@manipulate for inverse_aspect_ratio=0.4, plasma_elongation=1.4, triangularity=2.0, minor_radius=1.0, vacuum_Bϕ_on_axis=2.6, E=E_array, μ=μ_array, Pϕ=Pϕ_array\n",
    "\n",
    "    # Re-written from Mirko Salewski's solovev.m MATLAB script\n",
    "    maxx=1.2   #plasma in x in [-1;1], plot a little wider\n",
    "    cellsize=0.02\n",
    "    x=-maxx:cellsize:maxx\n",
    "    y=-plasma_elongation*maxx:cellsize:plasma_elongation*maxx\n",
    "    X_mesh = x' .* ones(length(y)) # Meshgrid\n",
    "    Y_mesh = ones(length(x))' .* y # Meshgrid\n",
    "\n",
    "    R0 = minor_radius/inverse_aspect_ratio\n",
    "\n",
    "    Rvec=R0*ones(length(x)) .+ minor_radius .* collect(x)\n",
    "    zvec=minor_radius .* collect(y)\n",
    "\n",
    "    R_mesh = Rvec' .*ones(length(zvec)) # Meshgrid\n",
    "    z_mesh = ones(length(Rvec))' .* zvec # Meshgrid\n",
    "\n",
    "    # CALCULATE PSI. The Solov'ev formula.\n",
    "    Ψ_norm = (X_mesh - 0.5*inverse_aspect_ratio .*(ones(size(X_mesh)) - X_mesh.^2)).^2 +(1 - 0.25*inverse_aspect_ratio^2) .*(ones(size(X_mesh)) + inverse_aspect_ratio*triangularity .* X_mesh .*(2 .*ones(size(X_mesh)) + inverse_aspect_ratio .*X_mesh)) .* (Y_mesh ./plasma_elongation).^2\n",
    "    Ψ_scale = (minor_radius^2)*vacuum_Bϕ_on_axis /(2*pi)\n",
    "    Ψ_matrix = Ψ_scale .* Ψ_norm\n",
    "\n",
    "    # CALCULATE MAGNETIC FIELD\n",
    "    dψdR = zeros(size(Ψ_matrix))\n",
    "    dψdR[1:end-1,:] .= diff(Ψ_matrix,dims=1) # dims=1 => R\n",
    "    dψdR[end,:] .= dψdR[end-1,:] # Set last row to same as second last\n",
    "    dψdR = (1/(abs(Rvec[2]-Rvec[1]))) .* dψdR # Scale\n",
    "    dψdz = zeros(size(Ψ_matrix))\n",
    "    dψdz[:,1:end-1] .= diff(Ψ_matrix,dims=2) # dims=2 => z\n",
    "    dψdz[:,end] .= dψdz[:,end-1] # Set last column to same as second last\n",
    "    dψdz = (1/(abs(zvec[2]-zvec[1]))) .* dψdz # Scale\n",
    "\n",
    "    BR = (-1) .* dψdz ./ R_mesh\n",
    "    Bz = dψdR ./ R_mesh\n",
    "    RBϕ_matrix = (vacuum_Bϕ_on_axis * R0) .* ones(size(R_mesh))\n",
    "    Bϕ = RBϕ_matrix ./ R_mesh\n",
    "    B_matrix = sqrt.(BR.^2 + Bϕ.^2 + Bz.^2)\n",
    "\n",
    "    Pϕ_matrix = Pϕ .* ones(size(B_matrix))\n",
    "    E_matrix = (E*1000*(GuidingCenterOrbits.e0)) .* ones(size(B_matrix)) # 1000*q is to convert from keV to Joule\n",
    "    μ_matrix = map(μ_func, E_matrix, B_matrix, Pϕ_matrix, Ψ_matrix, RBϕ_matrix)\n",
    "\n",
    "    #cross-sectional plot\n",
    "    cl = Contour.contour(Rvec,zvec,μ_matrix',μ)\n",
    "    l = Contour.lines(cl)\n",
    "    good_Rms = []\n",
    "    good_zms = []\n",
    "    for li=1:length(l)\n",
    "        ll = l[li]\n",
    "        Rll, zll = Contour.coordinates(ll)\n",
    "        closed_curve_measure = sqrt((Rll[end]-Rll[1])^2  +(zll[end]-zll[1])^2)\n",
    "        closed_curve_ref = sqrt((Rll[end]-Rll[end-1])^2  +(zll[end]-zll[end-1])^2)\n",
    "        if isapprox(closed_curve_measure, closed_curve_ref, atol=1e-1)\n",
    "            Rmi = argmax(Rll)\n",
    "            Rm = Rll[Rmi]\n",
    "            zm = zll[Rmi]\n",
    "            push!(good_Rms,Rm)\n",
    "            push!(good_zms,zm)\n",
    "        end\n",
    "    end\n",
    "    plt_crs = Plots.contour(Rvec,zvec,μ_matrix, levels=[μ], label=\"\", aspect_ratio=:equal, xlabel=\"R [m]\", ylabel=\"z [m]\", linewidth=2.5, colorbar=false)\n",
    "    plt_crs = Plots.contour!(Rvec,zvec,Ψ_matrix, levels=50, label=\"\", linestyle=:dot, color=:grey, colorbar=false)\n",
    "    plt_crs = Plots.scatter!(good_Rms,good_zms,mc=:red, label=\"(Rm,zm) points\", colorbar=false)\n",
    "\n",
    "    vbox(vskip(1em),\n",
    "        hbox(Plots.plot(plt_crs))\n",
    "    )\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "588d697f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": "163b8021410a4b5a8b0329ed2413f72d",
   "lastKernelId": "0749fadb-caf6-4dd7-9960-923491dcff6e"
  },
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
