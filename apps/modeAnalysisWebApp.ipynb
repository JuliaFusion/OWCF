{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c80543c5",
   "metadata": {},
   "source": [
    "> **THE OWCF WEB APPLICATIONS ARE TEMPORARILY OUT-OF-ORDER!**"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b9be3d23",
   "metadata": {},
   "source": [
    "# modeAnalysisWebApp"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "564ef2e2",
   "metadata": {},
   "source": [
    "## Description:\n",
    "This notebook provides a web application to visualize resonances between fast ions and magnetohydrodynamic (MHD) Alfvén eigenmodes in an interactive and intuitive manner. The resonance criterion is based on the equation\n",
    "\n",
    "ω = n ω_ϕ + m ω_θ + ω_rot\n",
    "\n",
    "where ω is the mode (angular) frequency, n is the toroidal mode number, ω_ϕ is the toroidal transit (angular) frequency of the fast ion, m is the poloidal mode number, ω_θ is the poloidal transit (angular) frequency of the fast ion and ω_rot is the plasma rotation (angular) frequency. ω_ϕ and ω_θ are computed by integrating the guiding-centre equations of motion, and then evaluating the toroidal (τ_ϕ) and poloidal (τ_θ) transit times. ω, n and m can be interactively changed by the user using the sliders in this web application.\n",
    "\n",
    "τ_ϕ and τ_θ are related to ω_ϕ and ω_θ as ω_ϕ = 2*pi/τ_ϕ and ω_θ = 2*pi/τ_θ, respectively.\n",
    "\n",
    "The resonance between mode and fast ion is then plotted in (E,pm,Rm) (or, via the press of a button in the app, in (E,Λ,Pϕ_n;σ)) and is visualized as a heatmap where the values (A) of the figure colorbar are computed according to the equation\n",
    "\n",
    "A = - log10(|ω - n ω_ϕ - m ω_θ - ω_rot|)\n",
    "\n",
    "where larger values of A thus corresponds to greater resonance, and smaller values of A corresponds to less resonance.\n",
    "\n",
    "### PLEASE NOTE! \n",
    "To be able to use the app, a topological map containing toroidal (tau_t) and poloidal (tau_p) transit time data has to be provided. This type of data is to be provided as a .jld2 file with the keys 'torTransTimes' and 'polTransTimes'. Such a file can be easily obtained by using the calcTopoMap.jl OWCF tool.\n",
    "\n",
    "## Inputs:\n",
    "- enable_COM - If true, (E,pm,Rm) -> (E,Λ,Pϕ_n;σ) can be performed via a toggle button. Set to false to minimize computation time - Bool\n",
    "- folderpath_OWCF - The path to the OWCF folder on your computer. Needed for correct loading - String\n",
    "- filepath_equil - The path to the file with the tokamak magnetic equilibrium and geometry - String\n",
    "- filepath_tm - The path to the .jld2-file containing the topological map (and toroidal and poloidal transit times) - String\n",
    "- FI_species - The species of the particle being simulated (deuterium, tritium etc) - String\n",
    "- ω_init - The mode frequency to investigate (in kHz). This can be interactively changed later in the app - Float64\n",
    "- ω_rot_init - The plasma rotation frequency (in kHz). This can be interactively changed later in the app - Float64\n",
    "- n - The toroidal mode number to investigate. This can be interactively changed later in web application - Int64\n",
    "- m - The toroidal mode number to investigate. This can be interactively changed later in web application - Int64\n",
    "- verbose - If set to true, the app will talk a lot! - Bool\n",
    "\n",
    "## Outputs:\n",
    "# -\n",
    "\n",
    "## Saved files:\n",
    "# -\n",
    "\n",
    "## Other:\n",
    "### Warning! Please note! \n",
    "For topological maps containing more than approximately 150 000 valid orbits (e.g. 20x100x100), you should NOT use modeAnalysisWebApp.jl (or any other interactive app). As of the current OWCF version, the app interface simply becomes too slow. Please do instead plot resonances manually. This can be done as shown for e.g. an (n,m)=(1,2) mode with ω=150 kHz in the cell below (if you correctly specify the folderpath_OWCF, filepath_tm, n, m, ω, and E variables, the cell will be able to run and plot):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43dcda9c",
   "metadata": {},
   "outputs": [],
   "source": [
    "folderpath_OWCF = \"/path/to/the/OWCF/folder/\"\n",
    "filepath_tm = \"/path/to/a/topological/map/file.jld2\"\n",
    "n = 1 # Toroidal mode number\n",
    "m = 2 # Poloidal mode number\n",
    "ω = 150 # kHz Wave frequency in kHz\n",
    "E = 150 # keV Energy in keV\n",
    "\n",
    "\n",
    "\n",
    "cd(folderpath_OWCF)\n",
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "using JLD2\n",
    "using Plots\n",
    "\n",
    "myfile = jldopen(filepath_tm, false, false, false, IOStream)\n",
    "topoMap = myfile[\"topoMap\"]\n",
    "E_array = myfile[\"E_array\"]\n",
    "pm_array = myfile[\"pm_array\"]\n",
    "Rm_array = myfile[\"Rm_array\"]\n",
    "torTransTimes = myfile[\"torTransTimes\"]\n",
    "polTransTimes = myfile[\"polTransTimes\"]\n",
    "close(myfile)\n",
    "\n",
    "iE = argmin(abs.(E_array - E)) # Find the closest value to E in E_array\n",
    "npm = length(pm_array)\n",
    "nRm = length(Rm_array)\n",
    "\n",
    "resonance = -log10.((2*pi/(ω*1000)) .*ones(size(npm,nRm)) - n .*torTransTimes[iE,:,:] - m .*polTransTimes[iE,:,:])\n",
    "\n",
    "Plots.heatmap(Rm_array,pm_array,resonance,legend=false,xlabel=\"Rm [m]\", ylabel=\"pm\", title=\"E: $(round(E,digits=3)) keV\", fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a0584524",
   "metadata": {},
   "source": [
    "### Notebook written by Henrik Järleblad, henrikj@dtu.dk\n",
    "### Last maintained 2025-10-07"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c448a034",
   "metadata": {},
   "outputs": [],
   "source": [
    "# SPECIFY THE INPUTS IN THIS CELL\n",
    "\n",
    "# Please specify the OWCF folder and let the notebook change directory to the \n",
    "# OWCF folder when the cell below is executed. This is to be able to load the\n",
    "# correct versions of the Julia packages as specified in the Project.toml and \n",
    "# Manifest.toml files.\n",
    "folderpath_OWCF = \"\" # Finish with '/'\n",
    "\n",
    "enable_COM = false # Set to false for large grids (>10x100x100 in (E,pm,Rm)). The computation time simply becomes too large. Or...\n",
    "if enable_COM\n",
    "    filepath_tm_COM = \"\" # ...please specify an output of the os2com.jl script (which contains the key \"topoMap\")(and possibly \"polTransTimes\" and \"torTransTimes\"). Leave unspecified if orbitsWebApp.jl should compute the (E,pm,Rm) -> (E,Λ,Pϕ_n;σ) map itself.\n",
    "end\n",
    "filepath_equil = \"\"  # Example JET shot 96100 at 13s (53 minus 40): g96100/g96100_0-53.0012.eqdsk\" #\n",
    "filepath_tm = \"\"\n",
    "FI_species = \"\" # Example deuterium: \"D\"\n",
    "ω_init = 0.0 # Mode frequency. kHz\n",
    "ω_rot_init = 0.0 # Plasma rotation frequency. kHz\n",
    "n_init = 1 # Toroidal mode number\n",
    "m_init = 2 # Poloidal mode number\n",
    "verbose = true\n",
    "\n",
    "# You can specify additional entries in the dictionary below, to include more keyword arguments in \n",
    "# the guiding-centre orbit integration algorithm\n",
    "extra_kw_args = Dict(:limit_phi => true, :maxiter => 0, :toa => true)\n",
    "# limits the number of toroidal turns for orbits\n",
    "# The orbit integration algorithm will try progressively smaller timesteps these number of times"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c7d6aec6",
   "metadata": {},
   "outputs": [],
   "source": [
    "# RUN THE APP BY FIRST SELECTING THE ABOVE CELL AND PRESS Ctrl+Enter. THEN SELECT THIS CELL\n",
    "# AND PRESS Shift+Enter. MIGHT TAKE A MOMENT TO PREPARE+LOAD THE INTERACTIVE APP.\n",
    "\n",
    "cd(folderpath_OWCF)\n",
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "\n",
    "verbose && println(\"Loading packages... \")\n",
    "using Interact\n",
    "using EFIT\n",
    "using Equilibrium\n",
    "using JLD2\n",
    "using GuidingCenterOrbits\n",
    "using Plots\n",
    "using FileIO\n",
    "using Mux\n",
    "using WebIO\n",
    "include(folderpath_OWCF*\"misc/species_func.jl\")\n",
    "include(folderpath_OWCF*\"extra/dependencies.jl\")\n",
    "\n",
    "myfile = jldopen(filepath_tm,false,false,false,IOStream)\n",
    "if haskey(myfile,\"extra_kw_args\")\n",
    "    verbose && println(\"Found extra_kw_args data in the 'filepath_tm' data. Giving priority... \")\n",
    "    extra_kw_args = myfile[\"extra_kw_args\"]\n",
    "end\n",
    "close(myfile)\n",
    "\n",
    "# Loading tokamak equilibrium\n",
    "verbose && println(\"Loading magnetic equilibrium... \")\n",
    "M, wall, jdotb = nothing, nothing, nothing # Initialize global magnetic equilibrium variables\n",
    "try\n",
    "    global M; global wall; global jdotb # Declare global scope\n",
    "    M, wall = read_geqdsk(filepath_equil,clockwise_phi=false) # Assume counter-clockwise phi-direction\n",
    "    jdotb = M.sigma # The sign of the dot product between the plasma current and the magnetic field\n",
    "catch # Otherwise, assume magnetic equilibrium is a saved .jld2 file\n",
    "    global M; global wall; global jdotb; local myfile # Declare global scope and local scope for variables\n",
    "    myfile = jldopen(filepath_equil,false,false,false,IOStream)\n",
    "    M = myfile[\"S\"]\n",
    "    wall = myfile[\"wall\"]\n",
    "    close(myfile)\n",
    "    jdotb = (M.sigma_B0)*(M.sigma_Ip)\n",
    "end\n",
    "\n",
    "#########################################################################################\n",
    "# Read the .jld2-file for displaying the topology of orbit space\n",
    "verbose && println(\"Loading topological map... \")\n",
    "myfile = jldopen(filepath_tm,false,false,false,IOStream)\n",
    "topoMap = myfile[\"topoMap\"]\n",
    "E_array = myfile[\"E_array\"]\n",
    "pm_array = myfile[\"pm_array\"]\n",
    "Rm_array = myfile[\"Rm_array\"]\n",
    "if haskey(myfile,\"polTransTimes\")\n",
    "    polTransTimes = myfile[\"polTransTimes\"]\n",
    "    torTransTimes = myfile[\"torTransTimes\"]\n",
    "else\n",
    "    error(\"Toroidal and poloidal transit time data required for modeAnalysisWebApp.jl. Please re-run calcTopoMap.jl with saveTransitTimeMaps set to true, and use output file as filepath_tm input to modeAnalysisWebApp.jl.\")\n",
    "end\n",
    "close(myfile)\n",
    "\n",
    "#########################################################################################\n",
    "# Mapping topological map to (E,Λ,Pϕ_n;σ)\n",
    "if enable_COM && !(isfile(filepath_tm_COM))\n",
    "    verbose && println(\">>>>>> Mapping topological map from (E,pm,Rm) to (E,Λ,Pϕ_n;σ) <<<<<<... \")\n",
    "    topoMap_COM, E_array, Λ_array, Pϕ_n_array = os2COM(M, topoMap, Vector(E_array), pm_array, Rm_array, FI_species; nl=2*length(pm_array), npp=2*length(Rm_array), isTopoMap=true, verbose=verbose)\n",
    "elseif enable_COM && isfile(filepath_tm_COM)\n",
    "    verbose && println(\"Loading topological map in (E,Λ,Pϕ_n;σ) coordinates from filepath_tm_COM... \")\n",
    "    myfile = jldopen(filepath_tm_COM,false,false,false,IOStream)\n",
    "    topoMap_COM = myfile[\"topoMap\"]\n",
    "    E_array_COM = myfile[\"E_array\"]\n",
    "    Λ_array = myfile[\"Lambda_array_topoMap\"]\n",
    "    Pϕ_n_array = myfile[\"Pphi_n_array_topoMap\"]\n",
    "    close(myfile)\n",
    "    if !(E_array==E_array_COM)\n",
    "        error(\"Energy grid points in (E,pm,Rm) do not match energy grid points in (E,Λ,Pϕ_n;σ). Please correct and re-try.\")\n",
    "    end\n",
    "else\n",
    "    verbose && println(\"Switching (E,pm,Rm) -> (E,Λ,Pϕ_n;σ) will not be possible.\")\n",
    "end\n",
    "\n",
    "#########################################################################################\n",
    "# Mapping maps of the poloidal and toroidal transit times to (E,Λ,Pϕ_n;σ), if available\n",
    "if enable_COM && !(isfile(filepath_tm_COM))\n",
    "    verbose && println(\">>>>>> Mapping poloidal transit times from (E,pm,Rm) to (E,Λ,Pϕ_n;σ) <<<<<<... \")\n",
    "    valid_orbit_indices = findall(x-> (x!=9.0) && (x!=7.0), topoMap) # 9 and 7 are the integers representing invalid and lost orbits in the calcTopoMap.jl script, respectively. We don't want them.\n",
    "    polTransTimes_COM, E_array_pol, Λ_array_pol, Pϕ_n_array_pol = os2COM(M, valid_orbit_indices, polTransTimes, E_array, pm_array, Rm_array, FI_species; nl=2*length(pm_array), npp=2*length(Rm_array), verbose=verbose)\n",
    "    verbose && println(\">>>>>> Mapping toroidal transit times from (E,pm,Rm) to (E,Λ,Pϕ_n;σ) <<<<<<... \")\n",
    "    torTransTimes_COM, E_array_tor, Λ_array_tor, Pϕ_n_array_tor = os2COM(M, valid_orbit_indices, torTransTimes, E_array, pm_array, Rm_array, FI_species; nl=2*length(pm_array), npp=2*length(Rm_array), verbose=verbose)\n",
    "elseif enable_COM && isfile(filepath_tm_COM)\n",
    "    verbose && println(\"Loading maps of poloidal and toroidal transit times in (E,Λ,Pϕ_n;σ) coordinates from filepath_tm_COM... \")\n",
    "    myfile = jldopen(filepath_tm_COM,false,false,false,IOStream)\n",
    "    polTransTimes_COM = myfile[\"polTransTimes\"]\n",
    "    E_array_pol = myfile[\"E_array\"] # Very silly\n",
    "    Λ_array_pol = myfile[\"Lambda_array_polTransTimes\"]\n",
    "    Pϕ_n_array_pol = myfile[\"Pphi_n_array_polTransTimes\"]\n",
    "    torTransTimes_COM = myfile[\"torTransTimes\"]\n",
    "    E_array_tor = myfile[\"E_array\"] # Even more silly\n",
    "    Λ_array_tor = myfile[\"Lambda_array_torTransTimes\"]\n",
    "    Pϕ_n_array_tor = myfile[\"Pphi_n_array_torTransTimes\"]\n",
    "    close(myfile)\n",
    "else\n",
    "end\n",
    "\n",
    "#########################################################################################\n",
    "# Safety-checking\n",
    "if enable_COM\n",
    "    verbose && println(\"Checking that the (E,pm,Rm) -> (E,Λ,Pϕ_n;σ) mapping yielded roughly the same E-, Λ- and Pϕ_n-grid points for topoMap, polTransTimes and torTransTimes... \")\n",
    "    if !isapprox(E_array, E_array_pol)\n",
    "        error(\"Energy grid points of topological map were not (roughly) equal to those of the poloidal transit time map. This should be impossible. orbitsWebApp.jl needs to be corrected manually.\")\n",
    "    end\n",
    "    if !isapprox(E_array, E_array_tor)\n",
    "        error(\"Energy grid points of topological map were not (roughly) equal to those of the toroidal transit time map. This should be impossible. orbitsWebApp.jl needs to be corrected manually.\")\n",
    "    end\n",
    "end\n",
    "if enable_COM\n",
    "    verbose && println(\"Checking that (E,Λ,Pϕ_n;σ) grid points coincide for τ_ϕ and τ_θ... \")\n",
    "    if !isapprox(Λ_array_pol,Λ_array_tor)\n",
    "        error(\"Λ grid points did not approximately coincide for τ_ϕ and τ_θ. Something has gone terribly wrong. Please contact henrikj@dtu.dk or anvalen@dtu.dk.\")\n",
    "    end\n",
    "    if !isapprox(Pϕ_n_array_pol,Pϕ_n_array_tor)\n",
    "        error(\"Pϕ_n grid points did not approximately coincide for τ_ϕ and τ_θ. Something has gone terribly wrong. Please contact henrikj@dtu.dk or anvalen@dtu.dk.\")\n",
    "    end\n",
    "end\n",
    "\n",
    "#########################################################################################\n",
    "# Double-checking so nothing has NaNs in it\n",
    "if !(sum(isnan.(topoMap))==0)\n",
    "    topoMap = map(x-> isnan(x) ? 6.0 : x, topoMap)\n",
    "    @warn \"Topological map in (E,pm,Rm) had NaNs in it! orbitsWebApp.jl automatically set them to 6.0 (incomplete).\"\n",
    "end\n",
    "if enable_COM\n",
    "    if !(sum(isnan.(topoMap_COM))==0)\n",
    "        topoMap_COM = map(x-> isnan(x) ? 6.0 : x, topoMap_COM)\n",
    "        @warn \"Topological map in (E,Λ,Pϕ_n;σ) had NaNs in it! orbitsWebApp.jl automatically set them to 6.0 (incomplete).\"\n",
    "    end\n",
    "end\n",
    "#if !(sum(isnan.(polTransTimes))==0)\n",
    "#    polTransTimes = map(x-> isnan(x) ? 0.0 : x, polTransTimes)\n",
    "#    @warn \"Map of poloidal transit times in (E,pm,Rm) had NaNs in it! orbitsWebApp.jl automatically set them to 0.0.\"\n",
    "#end\n",
    "#if !(sum(isnan.(torTransTimes))==0)\n",
    "#    torTransTimes = map(x-> isnan(x) ? 0.0 : x, torTransTimes)\n",
    "#    @warn \"Map of toroidal transit times in (E,pm,Rm) had NaNs in it! orbitsWebApp.jl automatically set them to 0.0.\"\n",
    "#end\n",
    "#if enable_COM\n",
    "#    if !(sum(isnan.(polTransTimes_COM))==0)\n",
    "#        polTransTimes_COM = map(x-> isnan(x) ? 0.0 : x, polTransTimes_COM)\n",
    "#        @warn \"Map of poloidal transit times in (E,Λ,Pϕ_n;σ) had NaNs in it! orbitsWebApp.jl automatically set them to 0.0.\"\n",
    "#    end\n",
    "#    if !(sum(isnan.(torTransTimes_COM))==0)\n",
    "#        torTransTimes_COM = map(x-> isnan(x) ? 0.0 : x, torTransTimes_COM)\n",
    "#        @warn \"Map of toroidal transit times in (E,Λ,Pϕ_n;σ) had NaNs in it! orbitsWebApp.jl automatically set them to 0.0.\"\n",
    "#    end\n",
    "#end\n",
    "\n",
    "#########################################################################################\n",
    "# Inverting poloidal and toroidal transit time data, to enable computation of resonance\n",
    "# Then multiply with 2*pi to get ω_ϕ and ω_θ\n",
    "verbose && println(\"Inverting poloidal and toroidal transit time data, to compute ω_θ and ω_ϕ, respectively... \")\n",
    "\n",
    "bad_coords = findall(x-> (x==9.0 || x==6.0 || x==7.0), topoMap) # Invalid, incomplete or lost orbits\n",
    "enable_COM && (bad_coords_COM = findall(x-> (x==9.0 || x==6.0 || x==7.0), topoMap_COM))\n",
    "\n",
    "# Taking care of bad coordinates for the poloidal and toroidal transit times. NaNs will lead to those pixels not being plotted\n",
    "polTransTimes[bad_coords] .= NaN\n",
    "torTransTimes[bad_coords] .= NaN\n",
    "enable_COM && (polTransTimes_COM[bad_coords_COM] .= NaN)\n",
    "enable_COM && (torTransTimes_COM[bad_coords_COM] .= NaN)\n",
    "\n",
    "ω_θ = map(x-> !isnan(x) ? 2*pi/x : x, polTransTimes)\n",
    "ω_ϕ = map(x-> !isnan(x) ? 2*pi/x : x, torTransTimes)\n",
    "enable_COM && (ω_θ_COM = map(x-> !isnan(x) ? 2*pi/x : x, polTransTimes_COM))\n",
    "enable_COM && (ω_ϕ_COM = map(x-> !isnan(x) ? 2*pi/x : x, torTransTimes_COM))\n",
    "\n",
    "#########################################################################################\n",
    "verbose && println(\"Computing flux function on 100x100 (R,z)-grid... \")\n",
    "flux_r = range(extrema(wall.r)...,length=100)\n",
    "flux_z = range(extrema(wall.z)...,length=100)\n",
    "inds = CartesianIndices((length(flux_r),length(flux_z)))\n",
    "psi_rz = [M(flux_r[ind[1]], flux_z[ind[2]]) for ind in inds]\n",
    "psi_mag, psi_bdry = psi_limits(M)\n",
    "\n",
    "# Compute necessary quantities for COM tool\n",
    "if enable_COM\n",
    "    verbose && println(\"Computing necessary quantities for constants-of-motion option... \")\n",
    "    B0 = norm(Equilibrium.Bfield(M,magnetic_axis(M)...)) # Tesla\n",
    "    q = getSpeciesCharge(FI_species) # Coulomb\n",
    "    if psi_bdry==0\n",
    "        @warn \"The magnetic flux at the last closed flux surface (LCFS) is found to be 0 for the magnetic equilibrium in $(filepath_equil). Pϕ_n=Pϕ/(q*|Ψ_w|) where Ψ_w=Ψ(mag. axis) is assumed instead of Ψ_w=Ψ(LCFS).\"\n",
    "        Ψ_w_norm = abs(psi_axis)\n",
    "    else\n",
    "        Ψ_w_norm = abs(psi_bdry)\n",
    "    end\n",
    "end\n",
    "#########################################################################################\n",
    "## --------------------------------------------------------------------------\n",
    "# The web application\n",
    "R_hfs = minimum(wall.r) # R-coord of high-field side wall\n",
    "R_lfs = maximum(wall.r) # R-coord of low-field side wall\n",
    "phi = collect(0:1:359).*(2*pi/180.0) # Toroidal angle\n",
    "topview_R_hfs_x = (R_hfs).*cos.(phi)\n",
    "topview_R_hfs_y = (R_hfs).*sin.(phi)\n",
    "topview_R_lfs_x = (R_lfs).*cos.(phi)\n",
    "topview_R_lfs_y = (R_lfs).*sin.(phi)\n",
    "\n",
    "E_array = vec(collect(E_array)) # Ensure type Array{Float64,1} (vector)\n",
    "pm_array = vec(collect(pm_array)) # Ensure type Array{Float64,1} (vector)\n",
    "Rm_array = vec(collect(Rm_array)) # Ensure type Array{Float64,1} (vector)\n",
    "\n",
    "@manipulate for E=E_array, pm=pm_array, Rm=Rm_array, ω=ω_init, ω_rot=ω_rot_init, n=n_init, m=m_init, phase_space = Dict(\"(E,Λ,Pϕ_n;σ)\" => :COM, \"(E,pm,Rm)\" => :OS), tokamak_wall = Dict(\"on\" => true, \"off\" => false), show_coordinate = Dict(\"on\" => true, \"off\" => false), save_plots = Dict(\"on\" => true, \"off\" => false)\n",
    "\n",
    "    EPRc = EPRCoordinate(M, E, pm, Rm; amu=getSpeciesAmu(FI_species), q=getSpeciesEcu(FI_species))\n",
    "    o = get_orbit(M,EPRc; wall=wall, extra_kw_args...)\n",
    "    if (phase_space==:COM) && enable_COM\n",
    "        myHc = HamiltonianCoordinate(M, EPRc)\n",
    "        μ = myHc.mu; Λ = μ*B0/E_joule\n",
    "        Pϕ = myHc.p_phi; Pϕ_n = Pϕ/(q*Ψ_w_norm)\n",
    "    end\n",
    "    \n",
    "    topview_o_x = cos.(o.path.phi).*(o.path.r)\n",
    "    topview_o_y = sin.(o.path.phi).*(o.path.r)\n",
    "\n",
    "    orb_color = :black\n",
    "    orb_linestyle = :solid\n",
    "    if o.class==:invalid\n",
    "        orb_color = :gray\n",
    "        orb_linestyle = :dash\n",
    "    elseif o.class == :lost\n",
    "        orb_color = :brown\n",
    "    elseif o.class == :incomplete # If this happens, you are scr*w*d. Because it will likely take forever to calculate.\n",
    "        orb_color = :yellow\n",
    "    elseif o.class == :trapped\n",
    "        orb_color = :blue\n",
    "    elseif o.class == :co_passing\n",
    "        orb_color = :green\n",
    "    elseif (o.class == :stagnation && o.coordinate.r>=magnetic_axis(M)[1]) # Regular stagnation orbit\n",
    "        orb_color = :red\n",
    "    elseif o.class == :potato\n",
    "        orb_color = :orange\n",
    "    elseif o.class == :ctr_passing\n",
    "        orb_color = :purple\n",
    "    elseif (o.class == :stagnation && o.coordinate.r<magnetic_axis(M)[1]) # Counter-stagnation orbit\n",
    "        orb_color = :pink\n",
    "    else\n",
    "        error(\"Something's gone wrong!!! Orbit class unknown!\")\n",
    "    end\n",
    "\n",
    "    #topview plot\n",
    "    plt_top = Plots.plot(topview_o_x,topview_o_y,label=\"$(o.class) orbit\", color=orb_color, linestyle=orb_linestyle, linewidth=1.5)\n",
    "    plt_top = Plots.plot!(topview_R_lfs_x,topview_R_lfs_y, label=\"Tokamak wall\", color=:black, linewidth=1.5, xlabel=\"x [m]\", ylabel=\"y [m]\")\n",
    "    plt_top = Plots.plot!(topview_R_hfs_x,topview_R_hfs_y, label=\"\", color=:black,linewidth=1.5, aspect_ratio=:equal, title=\"Top view\")\n",
    "    if save_plots\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            png(plt_top, \"plt_top_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        else\n",
    "            png(plt_top, \"plt_top_$(round(E, digits=2))_$(round(Λ, sigdigits=2))_$(round(Pϕ_n,sigdigits=2))\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    #cross-sectional plot\n",
    "    plt_crs = Plots.plot()\n",
    "    if tokamak_wall\n",
    "        wall_dR = maximum(wall.r)-minimum(wall.r)\n",
    "        plt_crs = Plots.contour!(flux_r,flux_z,psi_rz',levels=collect(range(psi_mag,stop=psi_bdry,length=5)),color=:gray, linestyle=:dot,linewidth=1.5, label=\"\",colorbar=false)\n",
    "        plt_crs = Plots.plot!(wall.r,wall.z, label=\"Tokamak wall\", color=:black, linewidth=1.5,xaxis=[minimum(wall.r)-wall_dR/10,maximum(wall.r)+wall_dR])\n",
    "    end\n",
    "    plt_crs = Plots.plot!(o.path.r,o.path.z, label=\"$(o.class) orbit\", color=orb_color, linestyle=orb_linestyle, linewidth=1.5)\n",
    "    if (phase_space==:OS) || !enable_COM\n",
    "        plt_crs = Plots.plot!(title=\"E: $(round(E,digits=2)) keV  pm: $(round(o.coordinate.pitch, digits=2))  Rm: $(round(o.coordinate.r,digits=2))\")\n",
    "    else # phase_space==:COM (OS = orbit space, COM = constants-of-motion)\n",
    "        plt_crs = Plots.plot!(title=\"E: $(round(E,digits=2)) keV  Λ: $(round(Λ, sigdigits=2))  Pϕ_n: $(round(Pϕ_n,sigdigits=2))\")\n",
    "    end\n",
    "    plt_crs = Plots.scatter!([magnetic_axis(M)[1]],[magnetic_axis(M)[2]],label=\"Magnetic axis\", mc=:gray, aspect_ratio=:equal, xlabel=\"R [m]\", ylabel=\" z [m]\")\n",
    "    plt_crs = Plots.scatter!([o.coordinate.r],[o.coordinate.z], mc=orb_color, label=\"(Rm,zm)\")\n",
    "    if save_plots\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            png(plt_crs, \"plt_crs_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        else\n",
    "            png(plt_crs, \"plt_crs_$(round(E, digits=2))_$(round(Λ, sigdigits=2))_$(round(Pϕ_n,sigdigits=2))\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    #topological plot(s)\n",
    "    Eci = argmin(abs.(collect(E_array) .- E))\n",
    "    if (phase_space==:OS) || !enable_COM\n",
    "        pm_array_ext = vcat(pm_array[1]-(diff(pm_array))[1],pm_array) # Extend pm_array one row below\n",
    "        topoMap_ext = vcat(vcat([1,2,3,4,5,6,7,8,9],ones(length(Rm_array)-9))', topoMap[Int64(Eci),:,:]) # Extend topoMap one row below, to ensure correct colormapping for orbit types (please see calcTopoMap.jl for more info). The y-limits (ylims) will make sure the extra row is not visible in the plot\n",
    "        plt_topo = Plots.heatmap(Rm_array,pm_array_ext,topoMap_ext,color=:Set1_9,legend=false,xlabel=\"Rm [m]\", ylabel=\"pm\", title=\"E: $(round(E,digits=3)) keV\", ylims=extrema(pm_array), xlims=extrema(Rm_array))\n",
    "        if tokamak_wall && (maximum(Rm_array) >= maximum(wall.r))\n",
    "            plt_topo = Plots.plot!(maximum(wall.r).*ones(length(pm_array)), pm_array, color=:black, linewidth=2)\n",
    "        end\n",
    "    else\n",
    "        Λ_array_ext = vcat(Λ_array[1]-(diff(Λ_array))[1],Λ_array) # Extend Λ_array one row below\n",
    "        if pm<0.0\n",
    "            topoMap_COM_ext = vcat(vcat([1,2,3,4,5,6,7,8,9],ones(length(Pϕ_n_array)-9))', topoMap_COM[Int64(Eci),:,:,1]) # Extend topoMap_COM one row below, to ensure correct colormapping for orbit types (please see calcTopoMap.jl for more info). The y-limits (ylims) will make sure the extra row is not visible in the plot\n",
    "            plt_topo = Plots.heatmap(Pϕ_n_array,Λ_array_ext,topoMap_COM_ext,color=:Set1_9,legend=false,xlabel=\"Pϕ_n\", ylabel=\"Λ\", title=\"E: $(round(E_array[Int64(Eci)],digits=3)) keV\", ylims=extrema(Λ_array), xlims=extrema(Pϕ_n_array))\n",
    "        else\n",
    "            topoMap_COM_ext = vcat(vcat([1,2,3,4,5,6,7,8,9],ones(length(Pϕ_n_array)-9))', topoMap_COM[Int64(Eci),:,:,2]) # Extend topoMap_COM one row below, to ensure correct colormapping for orbit types (please see calcTopoMap.jl for more info). The y-limits (ylims) will make sure the extra row is not visible in the plot\n",
    "            plt_topo = Plots.heatmap(Pϕ_n_array,Λ_array_ext,topoMap_COM_ext,color=:Set1_9,legend=false,xlabel=\"Pϕ_n\", ylabel=\"Λ\", title=\"E: $(round(E_array[Int64(Eci)],digits=3)) keV\", ylims=extrema(Λ_array), xlims=extrema(Pϕ_n_array))\n",
    "        end\n",
    "    end\n",
    "    if show_coordinate\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            plt_topo = Plots.scatter!([Rm],[pm],markershape=:circle,mc=orb_color,legend=false,markersize=6)\n",
    "        else\n",
    "            plt_topo = Plots.scatter!([Pϕ_n],[Λ],markershape=:circle,mc=orb_color,legend=false,markersize=6)\n",
    "        end\n",
    "    end\n",
    "    if save_plots\n",
    "        plt_topo = Plots.plot!(dpi=600)\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            png(plt_topo, \"plt_topo_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        else\n",
    "            png(plt_topo, \"plt_topo_$(round(E, digits=2))_$(round(Λ, sigdigits=2))_$(round(Pϕ_n,sigdigits=2))\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Resonance plot\n",
    "    if (phase_space==:OS) || !enable_COM\n",
    "        npm = length(pm_array)\n",
    "        nRm = length(Rm_array)\n",
    "        resonance = -log10.(abs.((ω*1000) .*ones(npm,nRm) - n .*ω_ϕ[Eci,:,:] - m .*ω_θ[Eci,:,:] - (ω_rot*1000) .*ones(npm,nRm)))\n",
    "        plt_res = Plots.heatmap(Rm_array,pm_array,resonance,xlabel=\"Rm [m]\", ylabel=\"pm\", title=\"E: $(round(E,digits=3)) keV\", fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]), ylims=extrema(pm_array), xlims=extrema(Rm_array))\n",
    "    else\n",
    "        nl = length(Λ_array_tor) # tor or pol should not matter\n",
    "        npp = length(Pϕ_n_array_pol) # tor or pol should not matter\n",
    "        if pm<0.0\n",
    "            resonance = -log10.(abs.((ω*1000) .*ones(nl,npp) - n .*ω_ϕ_COM[Int64(Eci),:,:,1] - m .*ω_θ_COM[Int64(Eci),:,:,1] - (ω_rot*1000) .*ones(nl,npp)))\n",
    "        else\n",
    "            resonance = -log10.(abs.((ω*1000) .*ones(nl,npp) - n .*ω_ϕ_COM[Int64(Eci),:,:,2] - m .*ω_θ_COM[Int64(Eci),:,:,2] - (ω_rot*1000) .*ones(nl,npp)))\n",
    "        end\n",
    "\n",
    "        plt_res = Plots.heatmap(Pϕ_n_array_tor[Int64(Eci),:],Λ_array_tor[Int64(Eci),:],resonance,legend=false,xlabel=\"Pϕ_n\", ylabel=\"Λ\", title=\"E: $(round(E,digits=3)) keV\", fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]), ylims=extrema(Λ_array_tor), xlims=extrema(Pϕ_n_array_tor))\n",
    "    end\n",
    "    if show_coordinate\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            plt_res = Plots.scatter!([Rm],[pm],markershape=:circle,mc=orb_color,legend=false,markersize=6)\n",
    "        else\n",
    "            plt_res = Plots.scatter!([Pϕ_n],[Λ],markershape=:circle,mc=orb_color,legend=false,markersize=6)\n",
    "        end\n",
    "    end\n",
    "    if save_plots\n",
    "        plt_res = Plots.plot!(dpi=600)\n",
    "        if (phase_space==:OS) || !enable_COM\n",
    "            png(plt_res, \"plt_res_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        else\n",
    "            png(plt_res, \"plt_res_$(round(E, digits=2))_$(round(Λ, sigdigits=2))_$(round(Pϕ_n,sigdigits=2))\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Put all the plots together\n",
    "    vbox(vskip(1em),\n",
    "    hbox(Plots.plot(plt_res),Plots.plot(plt_topo)),\n",
    "    hbox(Plots.plot(plt_top), Plots.plot(plt_crs))\n",
    "    )\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
