{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "fddf2f68",
   "metadata": {},
   "source": [
    "# distrWebApp"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8b2a4eae",
   "metadata": {},
   "source": [
    "## Description:\n",
    "This notebook provides an application to visualize orbit-space distribution functions in an \n",
    "interactive and intuitive manner. It can also be used to visualize other 3D orbit-space \n",
    "quantities, such as transformed flux surfaces to orbit space etc (for orbit weight functions,\n",
    "please use weightsWebApp.ipynb or weightWebApp.ipynb. For topological maps and maps of the poloidal and toroidal transit times, please use orbitsWebApp.ipynb).\n",
    "\n",
    "It visualizes distribution functions as a function of orbit space (2D slices of 3D space). \n",
    "\n",
    "A second orbit-space distribution (or other quantity) can be specified. In that case, the app\n",
    "will show to slices side-by-side so that the two 3D quantities can be compared as the user\n",
    "scans through fast-ion energy slices.\n",
    "\n",
    "Prior to running this notebook, please make sure you have run the following scripts:\n",
    "- ps2os.jl (or equivalent)\n",
    "- F_os_1Dto3D.jl (or equivalent)\n",
    "- extractTopoBounds.jl\n",
    "\n",
    "And that you have noted the paths to the outputs. You will also need the following: \n",
    "- .eqdsk/.jld2-file containing tokamak magnetic equilibrium and geometry\n",
    "\n",
    "ALSO PLEASE NOTE! It is recommended to ensure that the distribution function and the topological boundaries have exactly the same dimensions and ranges, by using the same orbit-space grid to calculate the topological map in getTopoMap.jl in the first place.\n",
    "\n",
    "## Inputs:\n",
    "- verbose - If set to true, the app will talk a lot! - Bool\n",
    "- FI_species - The fast-ion particle species. Please see OWCF/misc/species_func.jl for available options - String\n",
    "- filepath_tb - The path to the .jld2-file containing the topological boundaries - String\n",
    "- filepath_equil - The path to the .eqdsk-file with the tokamak magnetic equilibrium and geometry - String\n",
    "- filepath_distr - The path to the 3D orbit-space fast-ion distribution to be visualized - String\n",
    "- filepath_distr_2 - The path to a second 3D orbit-space distribution (/quantity). To be compared to the first - String\n",
    "- filepath_no - The path to the 3D orbit-space fast-ion null-measurement region boundaries. Enabled via showNo - String\n",
    "\n",
    "There are also more inputs further down in the notebook. These inputs can be ignored, unless\n",
    "you are specifically visualizing flux surfaces in orbit space. The inputs are\n",
    "psi_value - The value between 0.0 and 1.0 corresponding to the flux surface you are visualizing in orbit space.\n",
    "\n",
    "## Outputs:\n",
    "# -\n",
    "\n",
    "## Saved files:\n",
    "# - \n",
    "\n",
    "## Warning! \n",
    "Please note! For orbit-space grids containing more than approximately 150 000 valid orbits (e.g. 20x100x100), you should NOT use distrWebApp.ipynb (or any other interactive app). As of the current OWCF version, the web interface simply becomes too slow. Please do instead plot the energy slices manually. This can be done as shown in the following cell (if you correctly specify the filepath_OWCF, filepath_distr and filepath_tb variables, the cell will be able to run and plot):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27c8fab3",
   "metadata": {},
   "outputs": [],
   "source": [
    "folderpath_OWCF = \"/the/path/to/the/OWCF/folder/\"\n",
    "filepath_distr = \"/path/to/a/fast-ion/distribution/file.jld2\"\n",
    "filepath_tb = \"/path/to/a/topological/boundaries/file.jld2\"\n",
    "\n",
    "cd(folderpath_OWCF)\n",
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "using JLD2\n",
    "using Plots\n",
    "\n",
    "myfile = jldopen(filepath_distr, false, false, false, IOStream)\n",
    "F_os_3D = myfile[\"F_os_3D\"]\n",
    "E_array = myfile[\"E_array\"]\n",
    "pm_array = myfile[\"pm_array\"]\n",
    "Rm_array = myfile[\"Rm_array\"]\n",
    "close(myfile)\n",
    "\n",
    "myfile = jldopen(filepath_tb,false,false,false,IOStream)\n",
    "topoBounds = myfile[\"topoBounds\"]\n",
    "close(myfile)\n",
    "\n",
    "E = 150.0 # Example of 150 keV\n",
    "iE = argmin(abs.(E_array - E)) # Find the closest value to E in E_array\n",
    "\n",
    "ones_carinds = findall(x-> x==1.0,topoBounds[iE,:,:])\n",
    "pm_scatvals_tb = zeros(length(ones_carinds))\n",
    "Rm_scatvals_tb = zeros(length(ones_carinds))\n",
    "for (ind,carinds) in enumerate(ones_carinds)\n",
    "     pm_scatvals_tb[ind] = pm_array[carinds[1]]\n",
    "     Rm_scatvals_tb[ind] = Rm_array[carinds[2]]\n",
    "end\n",
    "\n",
    "Plots.heatmap(Rm_array, pm_array, (F_os_3D[iE,:,:])./maximum(F_os_3D[iE,:,:]), colorbar=true, title=\"Fast-ion distribution slice  ($(round(maximum(F_os_3D[iE,:,:]), sigdigits=4)) = 1.0)\",fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]))\n",
    "Plots.scatter!(Rm_scatvals_tb,pm_scatvals_tb,markersize=ms,leg=false,markercolor=:black, xlabel=\"Rm [m]\", ylabel=\"pm\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cbc8d0f9",
   "metadata": {},
   "source": [
    "### Notebook written by Henrik Järleblad, henrikj@dtu.dk\n",
    "### Last maintained 2025-10-07"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d857512c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# SPECIFY THE INPUTS IN THIS CELL\n",
    "\n",
    "# Please specify the OWCF folder and let the notebook change directory to the \n",
    "# OWCF folder when the cell below is executed. This is to be able to load the\n",
    "# correct versions of the Julia packages as specified in the Project.toml and \n",
    "# Manifest.toml files.\n",
    "folderpath_OWCF = \"\" # Finish with '/'\n",
    "\n",
    "verbose = true # If set to true, the notebook will be very talkative!\n",
    "FI_species = \"\" # Example deuterium: \"D\"\n",
    "filepath_tb = \"\"\n",
    "filepath_equil = \"\"\n",
    "filepath_distr = \"\"\n",
    "filepath_distr_2 = \"\"\n",
    "(showNo = false) && (filepath_no = \"\")\n",
    "\n",
    "# If you want to visualize output from e.g. a reconstruction, it might be in 4D format.\n",
    "# The first dimension then denotes different reconstructions for different regularization parameter values\n",
    "# Use 'id' to select which reconstruction to visualize, if filepath_distr is a reconstruction output\n",
    "# Use 'id_2' to select which reconstruction to visualize, if filepath_distr_2 is a reconstruction output\n",
    "id = 1\n",
    "id_2 = 1\n",
    "\n",
    "# You can specify additional entries in the dictionary below, to include more keyword arguments in \n",
    "# the guiding-centre orbit integration algorithm\n",
    "extra_kw_args = Dict(:limit_phi => true, :maxiter => 0, :toa => true)\n",
    "# limits the number of toroidal turns for orbits\n",
    "# The orbit integration algorithm will try progressively smaller timesteps these number of times\n",
    "# toa stands for \"try only adaptive\" orbit trajectory integration algorithm"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e7466a4d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# RUN THE APP BY FIRST SELECTING THE ABOVE CELL AND PRESS Ctrl+Enter. THEN SELECT THIS CELL\n",
    "# AND PRESS Shift+Enter. MIGHT TAKE A MOMENT TO PREPARE+LOAD THE INTERACTIVE APP.\n",
    "\n",
    "cd(folderpath_OWCF)\n",
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "\n",
    "verbose && println(\"Loading Julia packages... \")\n",
    "using Interact\n",
    "using EFIT\n",
    "using Equilibrium\n",
    "using GuidingCenterOrbits\n",
    "using JLD2\n",
    "using OrbitTomography\n",
    "using SparseArrays\n",
    "using Plots\n",
    "using FileIO\n",
    "using Mux\n",
    "using WebIO\n",
    "include(folderpath_OWCF*\"misc/species_func.jl\")\n",
    "include(folderpath_OWCF*\"extra/dependencies.jl\")\n",
    "\n",
    "myfile = jldopen(filepath_tb,false,false,false,IOStream)\n",
    "if haskey(myfile,\"extra_kw_args\")\n",
    "    verbose && println(\"Found extra_kw_args data in the 'filepath_tb' data. Giving priority... \")\n",
    "    extra_kw_args = myfile[\"extra_kw_args\"]\n",
    "end\n",
    "close(myfile)\n",
    "\n",
    "## ------\n",
    "# Read the .jld2-file for displaying the topological boundaries of orbit space.\n",
    "# Pre-calculated with getTopoMap.jl followed by extractTopoBounds.jl.\n",
    "verbose && println(\"Loading topological boundaries... \")\n",
    "myfile = jldopen(filepath_tb,false,false,false,IOStream)\n",
    "topoBounds = myfile[\"topoBounds\"]\n",
    "close(myfile)\n",
    "\n",
    "## ------\n",
    "# Read the .jld2-file for displaying the null-region boundaries of orbit space.\n",
    "# Pre-calculated with extractnullOrbs.jl.\n",
    "if showNo\n",
    "    if !isfile(filepath_no)\n",
    "        error(\"Null-measurement boundaries set for plot (showNo=true). But filepath_no is invalid. Please correct and re-try.\")\n",
    "    end\n",
    "    verbose && println(\"Loading null-region boundaries... \")\n",
    "    myfile = jldopen(filepath_no,false,false,false,IOStream)\n",
    "    if haskey(myfile,\"nullOrbs_indices\")\n",
    "        verbose && println(\"Found null-orbit (4D) indices (pressumably computed with extractNullOrbs.jl). Loading... \")\n",
    "        nullOrbs_indices = myfile[\"nullOrbs_indices\"]\n",
    "    elseif haskey(myfile,\"nullOrbs_1D\") && haskey(myfile,\"og\")\n",
    "        verbose && println(\"Found null-orbit vector and orbit grid (pressumably computed with solveInverseProblem.jl). Loading... \")\n",
    "        nullOrbs_1D = myfile[\"nullOrbs_1D\"]\n",
    "        og_nb = myfile[\"og\"]\n",
    "    else\n",
    "        @warn \"Input null-orbits file filepath_no did not have known null-orbit keys. Please re-compute extractNullOrbits.jl with input variable 'include2Dto4D' set to true, and use its output file as new filepath_no. Or re-compute solveInverse.jl with autoDetectAndUseNullOrbs set to true and use its output file as new filepath_no.\"\n",
    "    end\n",
    "    close(myfile)\n",
    "end\n",
    "\n",
    "## ------\n",
    "# Load the 3D orbit-space fast-ion distribution, the slices of which will be visualized\n",
    "verbose && println(\"Loading 3D orbit-space fast-ion distribution... \")\n",
    "myfile = jldopen(filepath_distr,false,false,false,IOStream)\n",
    "mykeys = keys(myfile)\n",
    "psi3D = false # Please ignore. This will be switched on automatically.\n",
    "psi_value = 0.6\n",
    "if \"F_os_3D\" in mykeys\n",
    "    F_os_3D = myfile[\"F_os_3D\"]\n",
    "elseif \"psi_os_3D\" in mykeys\n",
    "    F_os_3D = myfile[\"psi_os_3D\"]\n",
    "    Rz_array = myfile[\"Rz_array\"]\n",
    "    psi3D = true\n",
    "elseif \"F_reconstructions_3D\" in mykeys\n",
    "    F_os_3D = myfile[\"F_reconstructions_3D\"]\n",
    "else\n",
    "    error(\"Error! Unknown orbit-space 3D object in 'filepath_distr'. Please manually check distrWebApp.jl for accepted keys.\")\n",
    "end\n",
    "if haskey(myfile,\"og\")\n",
    "    og = myfile[\"og\"]\n",
    "    E_array = vec(og.energy)\n",
    "    pm_array = vec(og.pitch)\n",
    "    Rm_array = vec(og.r)\n",
    "else\n",
    "    E_array = myfile[\"E_array\"]\n",
    "    pm_array = myfile[\"pm_array\"]\n",
    "    Rm_array = myfile[\"Rm_array\"]\n",
    "end\n",
    "if haskey(myfile,\"FI_species\")\n",
    "    FI_species = myfile[\"FI_species\"]\n",
    "end\n",
    "\n",
    "close(myfile)\n",
    "\n",
    "## ------\n",
    "# Trim distribution data if necessary\n",
    "if length(size(F_os_3D))==4\n",
    "    F_os_3D = F_os_3D[id,:,:,:]\n",
    "end\n",
    "\n",
    "## ------\n",
    "# Load the other 3D orbit-space fast-ion distribution, the slices of which will be visualized\n",
    "if !(filepath_distr_2===nothing)\n",
    "    verbose && println(\"Loading the second 3D orbit-space fast-ion distribution... \")\n",
    "    myfile = jldopen(filepath_distr_2,false,false,false,IOStream)\n",
    "    mykeys = keys(myfile)\n",
    "    psi3D_2 = false # Please ignore. This will be switched on automatically.\n",
    "    psi_value_2 = 0.6\n",
    "    if \"F_os_3D\" in mykeys\n",
    "        F_os_3D_2 = myfile[\"F_os_3D\"]\n",
    "    elseif \"psi_os_3D\" in mykeys\n",
    "        F_os_3D_2 = myfile[\"psi_os_3D\"]\n",
    "        Rz_array_2 = myfile[\"Rz_array\"]\n",
    "        psi3D_2 = true\n",
    "    elseif \"F_reconstructions_3D\" in mykeys\n",
    "        F_os_3D_2 = myfile[\"F_reconstructions_3D\"]\n",
    "    else\n",
    "        error(\"Error! Unknown orbit-space 3D object in 'filepath_distr_2'. Please manually check distrWebApp.jl for accepted keys.\")\n",
    "    end\n",
    "    if haskey(myfile,\"og\")\n",
    "        og_2 = myfile[\"og\"]\n",
    "        E_array_2 = vec(og_2.energy)\n",
    "        pm_array_2 = vec(og_2.pitch)\n",
    "        Rm_array_2 = vec(og_2.r)\n",
    "    else\n",
    "        E_array_2 = myfile[\"E_array\"]\n",
    "        pm_array_2 = myfile[\"pm_array\"]\n",
    "        Rm_array_2 = myfile[\"Rm_array\"]\n",
    "    end\n",
    "    if haskey(myfile,\"FI_species\")\n",
    "        FI_species = myfile[\"FI_species\"]\n",
    "    end\n",
    "    close(myfile)\n",
    "end\n",
    "\n",
    "## ----------\n",
    "# Double-check that both loaded fast-ion distributions have the same E-, pm- and Rm-arrays\n",
    "if !(filepath_distr_2===nothing)\n",
    "    # Trim second distribution data if necessary\n",
    "    if length(size(F_os_3D_2))==4\n",
    "        F_os_3D_2 = F_os_3D_2[id_2,:,:,:]\n",
    "    end\n",
    "    if !((E_array==E_array_2) && (pm_array==pm_array_2) && (Rm_array==Rm_array_2))\n",
    "        error(\"Orbit-space grid arrays do not match for the two specified fast-ion distributions. Please correct and re-try.\")\n",
    "    end\n",
    "end\n",
    "# Check that the size of the null-orbit orbit grid matches the size of F_os_3D (and, by extension, the size of F_os_3D_2, if defined)\n",
    "if showNo && (@isdefined og_nb)\n",
    "    if !(size(og_nb.orbit_index)==size(F_os_3D))\n",
    "        error(\"Sizes of orbit grids used for null orbits and fast-ion distribution(s) do not match. Please correct and re-try.\")\n",
    "    end\n",
    "end\n",
    "\n",
    "## ----------\n",
    "# Loading tokamak equilibrium\n",
    "verbose && println(\"Loading magnetic equilibrium... \")\n",
    "M, wall, jdotb = nothing, nothing, nothing # Initialize global magnetic equilibrium variables\n",
    "try\n",
    "    global M; global wall; global jdotb; global timepoint#; global timepoint_source # Declare global scope\n",
    "    M, wall = read_geqdsk(filepath_equil,clockwise_phi=false) # Assume counter-clockwise phi-direction\n",
    "    jdotb = M.sigma # The sign of the dot product between the plasma current and the magnetic field\n",
    "catch # Otherwise, assume magnetic equilibrium is a saved .jld2 file\n",
    "    global M; global wall; global jdotb; global timepoint; local myfile #; global timepoint_source\n",
    "    myfile = jldopen(filepath_equil,false,false,false,IOStream)\n",
    "    M = myfile[\"S\"]\n",
    "    wall = myfile[\"wall\"]\n",
    "    close(myfile)\n",
    "    jdotb = (M.sigma_B0)*(M.sigma_Ip)\n",
    "end\n",
    "\n",
    "#########################################################################################\n",
    "verbose && println(\"Computing flux function on 100x100 (R,z)-grid (to plot flux surfaces)... \")\n",
    "flux_r = range(extrema(wall.r)...,length=100)\n",
    "flux_z = range(extrema(wall.z)...,length=100)\n",
    "inds = CartesianIndices((length(flux_r),length(flux_z)))\n",
    "psi_rz = [M(flux_r[ind[1]], flux_z[ind[2]]) for ind in inds]\n",
    "psi_mag, psi_bdry = psi_limits(M)\n",
    "\n",
    "## ----------\n",
    "# Check that the dimensions of topoBounds and F_os_3D match, and match the E-, pm- and Rm-arrays.\n",
    "verbose && println(\"Checking that dimensions are consistent... \")\n",
    "if !(size(F_os_3D)==size(topoBounds)) || !(size(F_os_3D,1) == length(E_array)) || !(size(F_os_3D,2) == length(pm_array)) || !(size(F_os_3D,3) == length(Rm_array))\n",
    "    println(\"Size(F_os_3D/psi_os_3D): $(size(F_os_3D))\")\n",
    "    println(\"Size(topoBounds): $(size(topoBounds))\")\n",
    "    println(\"Length(E_array): $(length(E_array))\")\n",
    "    println(\"Length(pm_array): $(length(pm_array))\")\n",
    "    println(\"Length(Rm_array): $(length(Rm_array))\")\n",
    "    error(\"Dimensions of topoBounds and/or F_os_3D don't match given E-, pm- and/or Rm-arrays. Please correct and re-try.\")\n",
    "end\n",
    "\n",
    "if psi3D\n",
    "    verbose && println(\"Unzipping Rz_array... \")\n",
    "    psiR_array = zeros(size(Rz_array))\n",
    "    psiz_array = zeros(size(Rz_array))\n",
    "    for i=1:length(Rz_array)\n",
    "        psiR_array[i] = (Rz_array[i])[1]\n",
    "        psiz_array[i] = (Rz_array[i])[2]\n",
    "    end\n",
    "end\n",
    "\n",
    "if !(filepath_distr_2===nothing)\n",
    "    if psi3D_2\n",
    "        println(\"Unzipping second Rz_array... \")\n",
    "        psiR_array_2 = zeros(size(Rz_array_2))\n",
    "        psiz_array_2 = zeros(size(Rz_array_2))\n",
    "        for i=1:length(Rz_array_2)\n",
    "            psiR_array_2[i] = (Rz_array_2[i])[1]\n",
    "            psiz_array_2[i] = (Rz_array_2[i])[2]\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "verbose && println(\"Size of pm_array: $(size(pm_array))\")\n",
    "verbose && println(\"Size of Rm_array: $(size(Rm_array))\")\n",
    "verbose && println(\"Size of E_array: $(size(E_array))\")\n",
    "verbose && println(\"Size of F_os_3D(/psi_os_3D): $(size(F_os_3D))\")\n",
    "verbose && println(\"Size of topoBounds: $(size(topoBounds))\")\n",
    "\n",
    "## ------\n",
    "# Make use of the loaded null-orbits data and prepare it for app visualization format\n",
    "if showNo && (@isdefined nullOrbs_indices)\n",
    "    verbose && println(\"Creating 3D null-orbit matrix from loaded 4D null-orbit data... \")\n",
    "    nullOrbs = zeros(size(F_os_3D))\n",
    "    for i=1:length(nullOrbs_indices)\n",
    "        nullOrbs[nullOrbs_indices[i][2],nullOrbs_indices[i][3],nullOrbs_indices[i][4]] = 1.0\n",
    "    end\n",
    "end\n",
    "if showNo && (@isdefined og_nb)\n",
    "    verbose && println(\"Creating 3D null-orbit matrix from loaded 1D null-orbit data and pertaining orbit grid... \")\n",
    "    nullOrbs = map_orbits(og_nb, nullOrbs_1D, true)\n",
    "end\n",
    "\n",
    "E_array = vec(collect(E_array)) # Ensure type Array{Float64,1}\n",
    "pm_array = vec(collect(pm_array)) # Ensure type Array{Float64,1}\n",
    "Rm_array = vec(collect(Rm_array)) # Ensure type Array{Float64,1}\n",
    "\n",
    "\n",
    "## ------\n",
    "# Pre-compute f(E), if needed\n",
    "if !(filepath_distr_2===nothing)\n",
    "    verbose && println(\"Pre-computing f(E) for visualization and comparison of the two fast-ion distributions... \")\n",
    "    dE3D, dpm3D, dRm3D = OrbitTomography.get3DDiffs(E_array,pm_array,Rm_array)\n",
    "    fE_orig = dropdims(sum(F_os_3D .*dpm3D .*dRm3D,dims=(2,3)),dims=(2,3))\n",
    "    fE_2_orig = dropdims(sum(F_os_3D_2 .*dpm3D .*dRm3D,dims=(2,3)),dims=(2,3))\n",
    "\n",
    "    fE = fE_orig ./ (maximum(fE_orig)== 0.0 ? 1.0 : maximum(fE_orig))\n",
    "    fE_2 = fE_2_orig ./ (maximum(fE_2_orig)== 0.0 ? 1.0 : maximum(fE_2_orig))\n",
    "\n",
    "    fE_min, fE_max = extrema(fE)\n",
    "    fE_2_min, fE_2_max = extrema(fE_2)\n",
    "    if (round(log10(fE_max))-round(log10(fE_min)))!=0.0 || (round(log10(fE_2_max))-round(log10(fE_2_min)))!=0.0\n",
    "        y_scale = :log10\n",
    "        gi_fE = findall(x-> x>0.0, fE)\n",
    "        gi_fE_2 = findall(x-> x>0.0, fE_2)\n",
    "    else\n",
    "        y_scale = :identity\n",
    "        gi_fE = 1:length(fE)\n",
    "        gi_fE_2 = 1:length(fE_2)\n",
    "    end\n",
    "end\n",
    "## ------\n",
    "# Minimizing memory usage\n",
    "og_nb = nothing\n",
    "og = nothing\n",
    "og_2 = nothing\n",
    "# Add more variables that won't be used again here...\n",
    "\n",
    "## ------\n",
    "# Compute wall (R,z) data and (x,y) data\n",
    "R_hfs = minimum(wall.r) # R-coord of high-field side wall\n",
    "R_lfs = maximum(wall.r) # R-coord of low-field side wall\n",
    "phi = collect(0:1:359).*(2*pi/180.0) # Toroidal angle\n",
    "topview_R_hfs_x = (R_hfs).*cos.(phi)\n",
    "topview_R_hfs_y = (R_hfs).*sin.(phi)\n",
    "topview_R_lfs_x = (R_lfs).*cos.(phi)\n",
    "topview_R_lfs_y = (R_lfs).*sin.(phi)\n",
    "\n",
    "@manipulate for tokamak_wall = Dict(\"on\" => true, \"off\" => false), colorbar_scale = Dict(\"0.0-1.0\" => \"zero2one\", \"0.0-0.1\" => \"zero2aTenth\"), compare_distributions = Dict(\"On\" => true, \"Off\" => false), E=E_array, pm=pm_array, Rm=Rm_array, save_plots = Dict(\"on\" => true, \"off\" => false)\n",
    "\n",
    "    EPRc = EPRCoordinate(M, E, pm, Rm; amu=getSpeciesAmu(FI_species), q=getSpeciesEcu(FI_species))\n",
    "    o = get_orbit(M,EPRc; wall=wall, extra_kw_args...)\n",
    "    topview_o_x = cos.(o.path.phi).*(o.path.r)\n",
    "    topview_o_y = sin.(o.path.phi).*(o.path.r)\n",
    "\n",
    "    orb_color = :black\n",
    "    orb_linestyle = :solid\n",
    "    if o.class==:invalid\n",
    "        orb_color = :gray\n",
    "        orb_linestyle = :dash\n",
    "    elseif o.class == :lost\n",
    "        orb_color = :brown\n",
    "    elseif o.class == :incomplete # If this happens, you are unfortunately in trouble. Because it will likely take forever to calculate. Please just re-start the app instead.\n",
    "        orb_color = :yellow\n",
    "    elseif o.class == :trapped\n",
    "        orb_color = :blue\n",
    "    elseif o.class == :co_passing\n",
    "        orb_color = :green\n",
    "    elseif (o.class == :stagnation && o.coordinate.r>=magnetic_axis(M)[1]) # Regular stagnation orbit\n",
    "        orb_color = :red\n",
    "    elseif o.class == :potato\n",
    "        orb_color = :orange\n",
    "    elseif o.class == :ctr_passing\n",
    "        orb_color = :purple\n",
    "    elseif (o.class == :stagnation && o.coordinate.r<magnetic_axis(M)[1]) # Pinch (HFS stagnation) orbit\n",
    "        orb_color = :pink\n",
    "    else\n",
    "        error(\"Something's gone wrong!!! Orbit class unknown!\")\n",
    "    end\n",
    "\n",
    "    # Cross-sectional plot\n",
    "    plt_crs = Plots.scatter([magnetic_axis(M)[1]],[magnetic_axis(M)[2]],label=\"Magnetic axis\", mc=:grey, aspect_ratio=:equal, xlabel=\"R [m]\", ylabel=\" z[m]\")\n",
    "    plt_crs = Plots.plot!(o.path.r,o.path.z, label=\"$(o.class) orbit\", color=orb_color, linestyle=orb_linestyle, linewidth=1.5,title=\"E: $(round(E, digits=4)) keV  pm: $(round(o.coordinate.pitch, digits=2))  Rm: $(round(o.coordinate.r,digits=2))\")\n",
    "    if tokamak_wall\n",
    "        plt_crs = Plots.contour!(flux_r,flux_z,psi_rz',levels=collect(range(psi_mag,stop=psi_bdry,length=5)),color=:gray, linestyle=:dot,linewidth=1.5, label=\"\",colorbar=false)\n",
    "        plt_crs = Plots.plot!(wall.r,wall.z, label=\"JET wall\", color=:black, linewidth=1.5,xaxis=[1.5,5.5])\n",
    "    end\n",
    "    plt_crs = Plots.scatter!([o.coordinate.r],[o.coordinate.z], mc=orb_color, label=\"(Rm,zm)\")\n",
    "    if psi3D\n",
    "        plt_crs = Plots.scatter!(psiR_array,psiz_array, label=\"Flux surface (Ψ=$(psi_value))\", mc=:gray,ms=1.5)\n",
    "    end\n",
    "    if !(filepath_distr_2===nothing)\n",
    "        if psi3D_2 && compare_distributions\n",
    "            plt_crs = Plots.scatter!(psiR_array_2,psiz_array_2, label=\"Flux surface (Ψ=$(psi_value))\", mc=:gray13,ms=1.5)\n",
    "        end\n",
    "    end\n",
    "    if save_plots\n",
    "        png(plt_crs, \"plt_crs_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "    end\n",
    "        \n",
    "    # Find the E in E_array. For fast-ion slice plot.\n",
    "    Ei = (findall(x-> x==E,E_array))[1] # Should only be 1 exactly equal element\n",
    "\n",
    "    # Extract correct topological boundaries and convert to vectors (for scatter plot)\n",
    "    ones_carinds = findall(x-> x==1.0,topoBounds[Ei,:,:])\n",
    "    pm_scatvals_tb = zeros(length(ones_carinds))\n",
    "    Rm_scatvals_tb = zeros(length(ones_carinds))\n",
    "    for (ind,carinds) in enumerate(ones_carinds)\n",
    "        pm_scatvals_tb[ind] = pm_array[carinds[1]]\n",
    "        Rm_scatvals_tb[ind] = Rm_array[carinds[2]]\n",
    "    end\n",
    "\n",
    "    # Extract correct null boundaries and convert to vectors (for scatter plot)\n",
    "    if showNo && (@isdefined nullOrbs)\n",
    "        ones_carinds = findall(x-> x==1.0,nullOrbs[Ei,:,:])\n",
    "        pm_scatvals_nb = zeros(length(ones_carinds))\n",
    "        Rm_scatvals_nb = zeros(length(ones_carinds))\n",
    "        for (ind,carinds) in enumerate(ones_carinds)\n",
    "            pm_scatvals_nb[ind] = pm_array[carinds[1]]\n",
    "            Rm_scatvals_nb[ind] = Rm_array[carinds[2]]\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if colorbar_scale == \"zero2one\"\n",
    "        clims = (0.0,1.0)\n",
    "    else\n",
    "        clims = (0.0,0.1)\n",
    "    end\n",
    "\n",
    "    #topview plot\n",
    "    plt_top = Plots.plot(topview_R_lfs_x,topview_R_lfs_y, label=\"JET wall\", color=:black, linewidth=1.5)\n",
    "    plt_top = Plots.plot!(topview_R_hfs_x,topview_R_hfs_y, label=\"\", color=:black,linewidth=1.5, aspect_ratio=:equal, title=\"Top view\")\n",
    "    plt_top = Plots.plot!(topview_o_x,topview_o_y,label=\"$(o.class) orbit\", color=orb_color, linestyle=orb_linestyle, linewidth=1.5)\n",
    "    if save_plots\n",
    "        png(plt_top, \"plt_top_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "    end\n",
    "\n",
    "    # distribution plot\n",
    "    plt_distr = Plots.heatmap(Rm_array, pm_array, (F_os_3D[Ei,:,:])./maximum(F_os_3D[Ei,:,:]), colorbar=true, title=\"Fast-ion distribution slice  ($(round(maximum(F_os_3D[Ei,:,:]), sigdigits=4)) = 1.0)\", clims=clims,fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]))\n",
    "    plt_distr = Plots.scatter!(Rm_scatvals_tb,pm_scatvals_tb,markersize=1.8,leg=false,markercolor=:white, xlabel=\"Rm [m]\", ylabel=\"pm\")\n",
    "    if showNo && (@isdefined nullOrbs)\n",
    "        plt_distr = Plots.scatter!(Rm_scatvals_nb,pm_scatvals_nb,markersize=5,leg=false,markercolor=:teal, markershape=:circle)\n",
    "    end\n",
    "    plt_distr = Plots.scatter!([Rm],[pm],markershape=:circle,mc=orb_color,markersize=5.0) # orbit coordinate marker\n",
    "    if save_plots\n",
    "        png(plt_distr, \"plt_distr_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "    end\n",
    "\n",
    "    #pitch visualization plot, or second distribution plot. Depending on input\n",
    "    if !(filepath_distr_2===nothing) && compare_distributions\n",
    "        # distribution plot\n",
    "        plt_pitc = Plots.heatmap(Rm_array, pm_array, (F_os_3D_2[Ei,:,:])./maximum(F_os_3D_2[Ei,:,:]), colorbar=true, title=\"Fast-ion reference slice  ($(round(maximum(F_os_3D_2[Ei,:,:]), sigdigits=4)) = 1.0)\", clims=clims,fillcolor=cgrad([:white, :darkblue, :green, :yellow, :orange, :red]))\n",
    "        plt_pitc = Plots.scatter!(Rm_scatvals_tb,pm_scatvals_tb,markersize=1.8,leg=false,markercolor=:white, xlabel=\"Rm [m]\", ylabel=\"pm\")\n",
    "        if showNo && (@isdefined nullOrbs)\n",
    "            plt_pitc = Plots.scatter!(Rm_scatvals_nb,pm_scatvals_nb,markersize=5,leg=false,markercolor=:teal, markershape=:circle)\n",
    "        end\n",
    "        plt_pitc = Plots.scatter!([Rm],[pm],markershape=:circle,mc=orb_color,markersize=5.0) # orbit coordinate marker\n",
    "        if save_plots\n",
    "            png(plt_pitc, \"plt_distr2_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        end\n",
    "    else\n",
    "        # pitch visualization plot\n",
    "        xmin = (pm >= 0.0) ? 0.0 : pm\n",
    "        xp = pm\n",
    "        yp = sqrt(1-pm^2)\n",
    "        plt_pitc = Plots.quiver([0.0],[0.0],quiver=([1.0],[0.0]), label=\"B field\", markerstrokewidth=4.0, color=:purple)\n",
    "        plt_pitc = Plots.plot!([0.0,1.0],[0.0,0.0], label=\"B field\", linewidth=3, color=:purple, annotations=(0.98,0.05,Plots.text(\"B\",:right)))\n",
    "        plt_pitc = Plots.quiver!([0.0],[0.0],quiver=([xp],[yp]), label=\"v\", markerstrokewidth=4.0, color=:black)\n",
    "        plt_pitc = Plots.plot!([0.0,xp],[0.0,yp], label=\"v\", linewidth=3, color=:black, annotations=(xp+0.02,yp+0.03,Plots.text(\"v\")))\n",
    "        plt_pitc = Plots.quiver!([0.0],[0.0],quiver=([0.0],[yp]), label=\"vperp\", markerstrokewidth=4.0, color=:red)\n",
    "        plt_pitc = Plots.plot!([0.0,0.0],[0.0,yp], label=\"vperp\", linewidth=3, color=:red, annotations=(0.02,yp+0.03,Plots.text(\"vperp\")))\n",
    "        plt_pitc = Plots.quiver!([0.0],[0.0],quiver=([xp],[0.0]), label=\"vpara\", markerstrokewidth=4.0, color=:blue)\n",
    "        plt_pitc = Plots.plot!([0.0,xp],[0.0,0.0], label=\"vpara\", xaxis=[xmin,1.0], yaxis=[0.0,1.0], linewidth=3, legend=true, color=:blue, fontsize=14.0, fontweight=\"bold\", annotations=(xp,0.05,Plots.text(\"vpara\")))\n",
    "        plt_pitc = Plots.plot!([0.0,xp],[yp,yp],linestyle=:dash,label=\"\",color=:black)\n",
    "        plt_pitc = Plots.plot!([xp,xp],[0.0,yp],linestyle=:dash,label=\"\",color=:black, title=\"pm: $(round(pm, digits=3))\")\n",
    "        if save_plots\n",
    "            png(plt_pitc, \"plt_pitc_$(round(E, digits=2))_$(round(o.coordinate.pitch, digits=2))_$(round(o.coordinate.r,digits=2))\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if !(filepath_distr_2===nothing) && compare_distributions\n",
    "        plt_fE = Plots.plot(E_array[gi_fE],fE[gi_fE],linewidth=3.0,color=:blue,label=\"f(E)\", yaxis=y_scale)\n",
    "        plt_fE = Plots.plot!(E_array[gi_fE_2],fE_2[gi_fE_2],linewidth=3.0,color=:black,label=\"f(E) reference\",xlabel=\"Energy [keV]\",ylabel=\"f(E) [keV^-1]\")\n",
    "        plt_fE = Plots.scatter!([E_array[Ei]],[fE[Ei]],ms=5.0,mc=:blue,label=\"$(round(fE_orig[Ei],digits=2))\")\n",
    "        plt_fE = Plots.scatter!([E_array[Ei]],[fE_2[Ei]],ms=5.0,mc=:black,label=\"$(round(fE_2_orig[Ei],digits=2))\", title=\"Normalized f(E) distributions\")\n",
    "        plt_fE = Plots.plot!(size=(1200,400),left_margin=5Plots.mm,bottom_margin=5Plots.mm)\n",
    "    end\n",
    "    \n",
    "    if !(filepath_distr_2===nothing) && compare_distributions\n",
    "        vbox(vskip(1em),\n",
    "            plt_fE,\n",
    "            hbox(plt_distr,plt_pitc),\n",
    "            hbox(plt_top,plt_crs)\n",
    "        )\n",
    "    else\n",
    "        vbox(vskip(1em),\n",
    "            hbox(plt_distr,plt_pitc),\n",
    "            hbox(plt_top,plt_crs)\n",
    "        )\n",
    "    end\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
